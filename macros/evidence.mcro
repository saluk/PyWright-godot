set bus_name Bus
set bus_description A bus

#@ Adds an evidence to the court record and shows an animation to the player about that evidence.
#$_ev - evidence id to reveal and add to court record
#$_description - description to display to the player about the evidence
#$_name - the name of the evidence to show to the player
macro addevanim
obj general/evidence/addevanim x=240 y=46 name=fg
ev $_ev x=240 y=50 name=ev
textblock 240 70 154 45 color=000 name=text $_description
textblock 240 55 154 17 color=950 name=text2 $_name
scroll x=-240 speed=20 name=fg nowait
scroll x=-235 speed=20 name=ev nowait
scroll x=-147 speed=20 name=text nowait
scroll x=-147 speed=20 name=text2 nowait
pause 1
nt
addev $_ev		
"{sound 0%}{spd2}{sfx evidence/added.ogg}{center}{c950}{$_name}{c098} added to the court record"
scroll x=-480 speed=20 name=fg nowait
scroll x=-480 speed=20 name=ev nowait
scroll x=-480 speed=20 name=text nowait
scroll x=-480 speed=20 name=text2 
pause 60
delete name=fg
delete name=ev
delete name=text
delete name=text2
endmacro

#@ pops up en evidence icon on the left side of the screen
macro showevl
sfx evidence/ev.ogg
obj general/evidence/showevl name=fg
pause 13
ev $_ev x=13 y=13 name=ev
delete name=fg
endmacro

#@ makes the upper left evidence icon pop away
macro hideevl
sfx evidence/ev.ogg
delete name=ev
obj general/evidence/hideevl x=0 y=0 name=fg
pause 13
delete name=fg
endmacro

#@ pops up en evidence icon on the right side of the screen
macro showevr
sfx evidence/ev.ogg
obj general/evidence/showevr x=0 y=0 name=fg nowait
pause 13
ev $_ev x=173 y=13 name=ev
delete name=fg
endmacro

#@ makes the upper right evidence icon pop away
macro hideevr
sfx evidence/ev.ogg
delete name=ev
obj general/evidence/hideevr x=0 y=0 name=fg
pause 13
delete name=fg
endmacro